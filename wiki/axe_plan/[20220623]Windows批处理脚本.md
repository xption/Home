
# 基础命令

## 控制命令回显

命令回显是指在命令行中显示该命令本身。注意：关闭回显只是不显示命令本身，命令的输出结果仍然会显示。通常，除非调试脚本，否则不需要回显命令，所以在脚本开头就会设置@echo off。

- @：关闭该行命令的回显
- echo on/off：控制之后所有命令的回显

```bat
@rem 关闭之后所有命令回显，但是echo off命令本身还是会显示
echo off

@rem 关闭之后所有命令回显，echo off本身也不显示（被@隐藏）
@echo off

@rem 显示echo是on还是off
echo

@rem 输出一个换行（echo与.之间没有空格）
echo.
```

## 注释rem

rem 命令表示当前行是注释。通常，注释是不需要回显的，所以总是在rem前面加上@。

```bat
rem 这是注释会回显
@rem 这条注释不会回显
```

## 暂停pause

bat脚本执行时会显示命令行，执行后默认会关闭该命令行。如果要查看脚本输出的结果，可以执行pause命令，提示：Press any key to continue...。这样可以暂停程序的运行，查看输出结果。

## 文件夹相关命令

```bat
@rem 显示目录内容
dir .
dir d:

@rem 显示目录所有内容
dir /a .
dir /a d:

@rem 以树形结构显示目录结构（不含文件）
tree .

@rem 以树形结构显示目录结构（含文件）
tree /f .

@rem 创建目录，md与mkdir等同
md 文件夹1
mkdir 文件夹2

@rem 删除目录
rd 文件夹1

@rem 重命名（将文件夹2重命名为文件夹3）
rename 文件夹2 文件夹3
```

## 文件相关命令

```bat
@rem 显示文件内容
type 1.txt
type *.txt

@rem 逐屏显示文件内容
more 1.txt

@rem 创建空文件（将type nul的结果输出到2.txt，而type nul的结果为空）
type nul > 2.txt

@rem 删除文件
del 1.txt

@rem 拷贝文件
copy 2.txt 3.txt

@rem 重命名
rename 3.txt 4.txt
```

## xcopy命令

```bat
@rem 复制文件夹1的内容到文件夹2
@rem /i 如果文件夹2不存在就创建
@rem /y 覆盖时不需要确认
@rem /s 包括子目录
@rem /e 包括子目录（包括空目录）
@rem /h 包括隐藏文件和系统文件
xcopy 文件夹1 文件夹2 /i/y/s/e/h
```

## 管道命令
```bat
@rem 管道命令用于将前一个命令的输出作为下一个命令的输入
type 1.txt | more
```

## 重定向命令

```bat
@rem 将abc输入到1.txt（覆盖）
echo abc > 1.txt

@rem 将def输入到1.txt（追加）
echo def >> 1.txt
```

## 参数

%0~%9用于获取传给批处理脚本的参数，其中%0是该批处理脚本本身，%1~%9是实际参数。%*可以获取参数%1~%9。

```bat
@rem 输出第一个参数
echo %1

@rem 输出所有参数
echo %*
```

## call命令

调用批处理程序，等被调用的批处理程序执行结束再继续执行。

```bat
@rem 调用其他批处理程序
call 2.bat
```

如果有多个脚本都用到同一个配置，可以将这个配置定义在一个公共脚本中，其他脚本call这个公共脚本。例如，将公共配置放在const.bat中，其他的脚本call const.bat：

```bat
@rem const.bat

set VSC_PATH=C:\Program Files\Microsoft VS Code\Code.exe
```

```bat
@rem start doc.bat，使用vscode打开xzdoc目录

call const.bat
start "" "%VSC_PATH%" xzdoc
exit
```

## start命令

start命令用于调用其他程序，不需要等程序执行结束就继续向下执行。这个命令平时用得最多，用批处理脚本启动vscode打开某个目录（我也就这点儿本事了）

```bat
@rem 启动一个新的命令行窗口，参数为窗口的title
start "New Window"

@rem 打开网页
start https://www.bing.com

@rem 调用计算器程序
start calc.exe

@rem 调用vscode打开当前目录
@rem /max /min 分别控制程序窗口的最大化、最小化显示，不过似乎无效
@rem 第一个参数是命令行窗口的title，不过似乎只有当调用的是命令行程序才会生效，一般为空
start "" "C:\Program Files\Microsoft VS Code\Code.exe" .
```

## 其他指令

```bat
@rem 清屏
cls

@rem 设置当前命令行窗口的标题
title 我的标题

@rem 显示日期，会提示输入新日期，按回车跳过
date
@rem 显示日期，不提示输入新日期
date /t

@rem 显示时间，会提示输入新时间，按回车跳过
time
@rem 显示时间，不提示输入新时间
time /t
```

## set命令

- 批处理中的变量不区分类型
- 通过在变量名两边加%获取变量值

```bat
@rem 定义变量
set a=tom
echo %a%
set a=20
echo %a%

@rem 删除变量
set a=

@rem 打印所有已定义变量（包括系统的环境变量）
set

@rem 获取环境变量GOPATH
echo %GOPATH%
```

## if语句

批处理中if语句通常写在一行，如果有多行或者命令带参数，需用小括号包围。

- not: 非
- equ：等于
- neq：不等于
- gtr：大于
- geq：大于等于
- lss：小于
- leq：小于等于

```bat
set a=10

@rem 写在一行，可读性差
if %a% equ 11 (echo equal) else (echo not equal)

@rem 分行写法
if %a% equ 11 (
    echo equal
) else (
    echo not_equal
)

@rem 判断文件或者目录是否存在
if exist 2.bat (
    echo exists
) else (
    echo not_exists
)
```

## for语句

```bat
@rem /l 表示从0到5，每次递增1遍历（包括0和5）
for /l %%i in (0, 1, 5) do echo %%i

@rem 遍历当前目录下的.bat .txt文件，输出文件内容
for %%c in (*.bat *.txt) do type %%c
```

## goto语句

因为批处理没有break指令，所以goto语句有用武之地。

```bat
@rem 冒号:用于定义label，goto跳转到指定的label
:label
goto label
```